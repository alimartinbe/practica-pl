
import java_cup.runtime.*;
import java.io.*;

import java.util.ArrayList;

parser code {:
	public static void main(String args[]) throws Exception {
	FileInputStream fichero=new FileInputStream(args[0]);
	DataInputStream entrada =new DataInputStream(fichero);
	try{
	new parser(new analizadorLexico(entrada)).parse();
	}
	catch ( Exception e) {
		System.out.println("Analisis INCORRECTO !!");
		System.exit(1);}
	System.out.println("Analisis Correcto ");
	
	}

    public void report_error(String message, Object info) {
        StringBuffer m = new StringBuffer("Error");
        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s=((java_cup.runtime.Symbol)info);
            /* Comprueba si el numero de línea es mayor o igual que cero */
            if (s.left >= 0) {
                m.append(" en linea "+(s.left+1));
            /*Comprueba si el numero de columna es mayoro igual que cero */
            if (s.right >= 0)
                m.append(", y columna "+(s.right+1));
            }
        }
        m.append(" : "+message);
        System.err.println(m);
    }   
    
:} ;

terminal ESCENARIO, INISEC, COMA, PARIZQ, PARDER, FINSEC, PLAN, POSACCIONES, INICIO;
terminal String FONDO, PERSONAJE, ACCION, OBJETO, POSICION, UBICACION;

non terminal juego, inicio, pos, rep, repe, restriccion, list, lista, par, para, plan, ac, acp, acc, acca, acce, acci;
non terminal Escenario escenario;
non terminal ArrayList<String> ob, obs;

juego ::= escenario:e inicio restriccion plan {: System.out.println(e.getLista().toString()); :};

escenario ::= ESCENARIO INISEC FONDO:f COMA PERSONAJE:p COMA PARIZQ ob:objeto PARDER FINSEC 
		{: RESULT = new Escenario(p, f, objeto); :}
        | error INISEC FONDO COMA PERSONAJE COMA PARIZQ ob PARDER FINSEC 
        {: parser.report_error("Falta la palabra reservada <escenario> al comienzo de la declaracion.", null); :}
        | ESCENARIO INISEC error COMA PERSONAJE COMA PARIZQ ob PARDER FINSEC {: parser.report_error("Falta el fondo en la declaración.", null); :}
        | ESCENARIO INISEC FONDO COMA error COMA PARIZQ ob PARDER FINSEC {: parser.report_error("Falta el personaje.", null); :};

ob ::= OBJETO:o obs:objeto1 {:  RESULT = objeto1;  RESULT.add(o); System.out.println(o);:}
        | error obs {: parser.report_error("Falta el objeto antes de la declaración de su posición.", null); :};

obs ::= COMA ob:o1 {: RESULT = o1;:}| {:ArrayList<String> lista = new ArrayList<String>(); RESULT = lista;:};

inicio ::= INICIO INISEC PERSONAJE pos COMA rep FINSEC
        | error INISEC PERSONAJE pos COMA rep FINSEC {: parser.report_error("Falta la palabra reservada <inicio> al comienzo de la declaración.", null); :}
        | INICIO INISEC error pos COMA rep FINSEC {: parser.report_error("Falta el personaje.", null); :}; 

pos ::= PARIZQ POSICION COMA UBICACION PARDER
        | error {: parser.report_error("Falta la declaración de la posición del personaje.", null); :}
        | PARIZQ POSICION COMA:c error PARDER {: parser.report_error("Falta la ubicacion del personaje.", null); :}
        | PARIZQ error COMA UBICACION PARDER {: parser.report_error("Falta la posicion del personaje.", null); :};

rep ::= OBJETO pos repe
        |error pos repe {: parser.report_error("Falta el objeto antes de la declaración de su posición.", null); :};

repe ::= COMA rep | ;

restriccion ::= POSACCIONES INISEC PERSONAJE PARIZQ par PARDER COMA list FINSEC 
        | error INISEC PERSONAJE PARIZQ par PARDER COMA list FINSEC 
        {: parser.report_error("Falta la palabra reservada <posacciones> al comienzo de la declaración.", null); :}
        | POSACCIONES INISEC error PARIZQ par PARDER COMA list FINSEC 
        {: parser.report_error("Falta el personaje.", null); :};

list ::= OBJETO PARIZQ par PARDER lista | error PARIZQ par PARDER lista 
                                        {: parser.report_error("Falta el objeto antes de su declaración de posibles acciones.", null); :};

lista ::= COMA list | ;

par ::= ACCION para | error para {: parser.report_error("Falta la accion.", null); :};

para ::= COMA par | ;

plan ::= PLAN INISEC ac FINSEC 
        | error INISEC ac FINSEC {: parser.report_error("Falta la palabra reservada <plan> al comienzo de la declaración.", null); :};

ac ::= acc acp;

acp ::= COMA ac | ;

acc ::= ACCION PARIZQ acca | error PARIZQ acca  {: parser.report_error("No se encuentra la siguiente acción.", null); :};

acca ::= PERSONAJE PARDER | POSICION COMA POSICION PARDER | OBJETO COMA POSICION acce;

acce ::= PARDER | COMA acci | COMA error {: parser.report_error("No se encuentra el siguiente elemento de posición.", null); :};

acci ::= POSICION PARDER | UBICACION PARDER | error PARDER {: parser.report_error("No se encuentra la posición o ubicación.", null); :};

